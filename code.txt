; TODO macrophages need to die eventually too
; Maybe after a period of time without killing a pathogen
; a lymphocyte turns into a supressor and tells anything
; using the same code to stop reproducing
breed [pathogens pathogen] ; foregin invaders in body
breed [lymphocytes lymphocyte] ; immune cells. Need activating
breed [macrophages macrophage] ; immune cells. Identify intruders & activate lymphocytes

globals [mutation-allowed]

pathogens-own [
  secret_code
]

macrophages-own [
  activated?
  learned_code
]

lymphocytes-own [
  learned_code
  activated?
  tick-count
  ticks-since-last-kill
]

to setup
  clear-all
  set-default-shape pathogens "bug"
  set-default-shape lymphocytes "square 2"

  add-new-pathogen

  produce-immune-cells
  
  set mutation-allowed mutation
  reset-ticks
end

to go
  ask pathogens [
    move
    reproduce-pathogen
  ]

  ask macrophages [
    move
    macrophage-detect-pathogen
    detect-lymphocyte
  ]

  ask lymphocytes [
    move
    reproduce-lymphocyte
    lymphocyte-detect-pathogen
    update-tick-count
    deactivate-lymphocyte
  ]
  
  ; This is to prevent runaway mutation
  ifelse count pathogens > 1000
    [set mutation-allowed false]
    [set mutation-allowed mutation]


  every 1 [produce-immune-cells]
  every 1 [ask n-of lymphocyte_production lymphocytes [die]]
  tick
end

to deactivate-lymphocyte
  if ticks-since-last-kill > 22 [
    set color white
    set activated? false
  ]
end

to add-new-pathogen
  ; set a 4 digit random code for the pathogens
  ; this represents their specific protein
  ; configuration that has to be learned by
  ; the immune system
  let pathogen_secret random 8999 + 1000
  create-pathogens pathogen_count [
    set color red
    setxy random-xcor random-ycor
    set secret_code pathogen_secret
  ]
end
to macrophage-detect-pathogen
  if not activated? [
    let nearest one-of pathogens-here
    if nearest != nobody [
      set learned_code [secret_code] of nearest
      set activated? true
      set color green
      ask nearest [die]
    ]
  ]
end

to lymphocyte-detect-pathogen
  if activated? [
    let nearest one-of pathogens-here
    if nearest != nobody [
      let pathogen_code [secret_code] of nearest
      if pathogen_code = learned_code [
        ask nearest [die]
        set ticks-since-last-kill 0
      ]
    ]
  ]
end

to detect-lymphocyte
  if activated? [
    let nearest one-of lymphocytes-here
    let my_code learned_code
    if nearest != nobody [
      ask nearest [set learned_code my_code
        set activated? true
        set color green
      ]
      ; after it passes on the knowledge of the pathogen the macrophage is used up
      die
    ]
  ]
end

to move
  rt random 50
  lt random 50
  fd 1
end

to reproduce-pathogen
  ; get the secret code of the parent
  let my_code secret_code
  ; ten percent chance of reproducing on every tick
  if random-float 100 <= 10 [
    hatch 1 [
      if mutation-allowed [
        ; 1/20th of a percent chance of mutation on every
        ; reproduction cycle
        ifelse random-float 100 <= .05 [
          print "suddenly a mutant appears"
          set secret_code random 8999 + 1000
        ]  [set secret_code my_code ]
      ]
      
      rt random-float 360 fd 1
    ]
  ]

  ; five percent chance of dying on every tick
  if random-float 100 <= 5 [
    die ]
end

to produce-immune-cells
  create-lymphocytes lymphocyte_production [
    set color white
    setxy random-xcor random-ycor
    set activated? false
    set tick-count 0
  ]

  ; Produces 1/8th the macrophages as lymphocytes
  ; and ensures at least one macrophage produced
  let macro-production max list (lymphocyte_production / 8) 1

  create-macrophages macro-production  [
    set color white
    setxy random-xcor random-ycor
    set activated? false
  ]
end
to reproduce-lymphocyte
  ; ten percent chance of reproducing on every tick
  ; this only applies to active lymphocytes that have
  ; been triggered to reproduce through mitosis. Unactivated
  ; lymphocytes are produced by the body.
  let my_code learned_code
  if activated? [
    if random-float 100 <= 10 [
      hatch 1 [
        set learned_code my_code
        set activated? true
        set color green
      ]
    ]
  ]
end

to update-tick-count
  set tick-count tick-count + 1
  ; keep track of how many ticks it has been since
  ; the lymphocyte saw and destroyed something
  set ticks-since-last-kill ticks-since-last-kill + 1
  ; lymphocytes have to die of old age eventually
  ; We say 3 seconds which is 45 ticks when the
  ; frame rate is 15 ticks/second
  if tick-count = 120 [die]
end